;zgig
;avance-version (12/01/11)
;1D sequence with inverse gated decoupling

; 140616. @YaroslavNikolaev. zgig002.yn: default setup - automatic calc of pulses (have to set -DNOAUTO to avoid auto-calc).
; 140224. @YaroslavNikolaev. added -DAUTO -DSP_AUTO flags & section for auto-calculation of parameters.

;added option NOHDEC to turn off H decoupling in acquisition

;$CLASS=HighRes
;$DIM=1D
;$TYPE=
;$SUBTYPE=
;$COMMENT=

#include <Avance.incl>

"d11=30m"
"acqt0=-p1*2/3.1416"
"plw0=0"

; ===================================== Automation =====================================
#ifndef NOAUTO
; Use this section to auto-calculate parameters not requiring fine-tuning (HARD pulses, power levels, delays, AU-parameters, etc).

; General procedure to calculate pulse power levels.
; powerlevel [dB] = 20*log(pulseLength*SHAPE_INTEGFAC/(P90H/90*SHAPE_TOTROT))/log(10)+dBH
; TopSpin for some reason does not recognize the call to dB level of shaped pulse: "spdb". Have to calcualte via whatts.
; powerlevel [whatts] = plw1*pow( (P90H/90)/((pulseLength/SHAPE_TOTROT)*SHAPE_INTEGFAC), 2)

; 1H decoupling of 31P pcpd2@pl12 (rect 70us 90deg):
; p3 90 degree 1H pulse has to be defined in ased
#ifndef NOHDEC
"plw12=plw2*pow( (p3/90)/(pcpd2/90), 2)" ; auto-calc in Watts
#endif
#endif /* NOAUTO */


#ifndef NOSP_AUTO
; Use this section to auto-calculate the power of SHAPED PULSES (assuming that in some cases one may want to fine-tune these)

#endif /* NOSP_AUTO */
; ===================================== End of Automation ===============================

1 ze
  d11 pl12:f2
2 30m do:f2
  d1
  p1 ph1
#ifdef NOHDEC
  go=2 ph31
#else
  go=2 ph31 cpd2:f2
#endif
  30m do:f2 mc #0 to 2 F0(zd)
; Could put the p3*0.001 (1H 90deg) definition here too (@FredD).
exit

10u pl0:f2
p3*0.0001:f2 ; This pulse is never used.
; This is just a trick to allow user to enter 1H hard pulse in ased window,
; (needed to auto-calculate the power level for 1H decoupling).

ph1=0 2 2 0 1 3 3 1
ph31=0 2 2 0 1 3 3 1

;pl1 : f1 channel - power level for pulse (default)
;p1 : f1 channel (31P) -  high power pulse

;pl0 : any channel - 0dB

;pl12: f2 channel - power level for CPD/BB decoupling
;p3 : f2 channel (1H) - 90 degree hard pulse 1H
;cpd2: decoupling according to sequence defined by cpdprg2
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence

;d1 : 0.8s (IVTNMR). relaxation delay; 1-5 * T1
;d11: delay for disk I/O                             [30 msec]
;ns: 1 * n, total number of scans: NS * TD0

;$Id: zgig,v 1.10.8.1 2012/01/31 17:56:42 ber Exp $
