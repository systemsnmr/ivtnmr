; @YN: this pulse-seq does not include automation for (decoupling) 13C pulses (yet)!!
; use rpar SFHMQCF3GPPH > and getprosol (see getbruk.yn for syntax)

; sfhmqc01.yn (@Yaroslav Nikolaev, 2015-04)
;   - added NOAUTO and NOSP_AUTO sections - automating calc of 15N decoupling & 1H selective pulses.

; try non-adiabatic CO decoupling - intensity of CO-coupled NH is very weak.

; ZGOPTNS
; NOAUTO
; NOSP_AUTO
; LABEL_CN

; Above is based on:
;sfhmqcf3gpph
;avance-version (13/02/28)
;SOFAST HMQC
;2D H-1/X correlation via heteronuclear zero and double quantum
;   coherence
;phase sensitive
;with decoupling during acquisition
;
;P.Schanda and B. Brutscher, J. Am. Chem. Soc. 127, 8014 (2005)
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=

; $OWNER=yaroslav

prosol relations=<triple>

#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"d11=30m"
"d12=20u"
"d21=1s/(cnst4*2)" ; inept delay [ 1/(2J) ]
"d16=200u" ; homospoil/gradient recovery

"in0=inf1"
"d0=in0/2-p21*4/3.1415"

; ===================================== Automation =====================================
#ifndef NOAUTO
; Use this section to auto-calculate parameters not requiring fine-tuning (HARD pulses, power levels, delays, AU-parameters, etc).

; General procedure to calculate pulse power levels.
; powerlevel [dB] = 20*log(pulseLength*SHAPE_INTEGFAC/(P90H/90*SHAPE_TOTROT))/log(10)+dBH
; TopSpin for some reason does not recognize the call to dB level of shaped pulse: "spdb". Have to calcualte via whatts.
; powerlevel [whatts] = plw1*pow( (P90H/90)/((pulseLength/SHAPE_TOTROT)*SHAPE_INTEGFAC), 2)
; SHAPE_INTEGFAC = 1 for rectangular pulses. For shaped pulses - should be indicated in the header of the shape file!

; Offset for selective pulses - get Hz from ppm value:
; cnst19 - offset (in ppm)
"spoff23=bf1*(cnst19/1000000)-o1"
"spoff24=bf1*(cnst19/1000000)-o1"

; For 120 degree Pc9_4_120.1000 pulse, normalization constant is 0.529 (See references in the header).
; If want to optimize this constant manually - see notes in e.g. zg-sofast004.yn.
"cnst39=0.529"

; Shaped pulse lengths.
; Pulse [us] = BWfactor (depends on the shape & angle) / Banwidth for excitation [Hz] * 1000000 (scaling for s->us).
; cnst63 - spectral width of signals of interest [~4.1 ppm for H(N)]

; Excitation 120 degree (Pc9_4_120.1000) pulse (##$SHAPE_BWFAC= 7.512000E00):
"p39=(7.512/(bf1*cnst63))*1000000"
; Refocusing 180 degree (Rsnob.1000) pulse (##$SHAPE_BWFAC= 2.332000E00):
"p40=(2.332/(bf1*cnst63))*1000000"

; Calc pl26 (decoupling power) based on p62

; 15N decoupling of p62 @ pl26 (rect ~350us 90deg):
; p21 90 degree 15N pulse has to be defined in ased
"plw26=plw3*pow( (p21/90)/(p62/90), 2)" ; auto-calc in Whatts

; Delays compensating for Chem.Shift evolution occurring during p39 selective pulse.
"DELTA1=d21-p16-d16-p39*cnst39"
"DELTA2=p39*cnst39-de-4u"
#endif /* NOAUTO */


#ifndef NOSP_AUTO
; Use this section to auto-calculate the power of SHAPED PULSES (assuming that in some cases one may want to fine-tune these)

; Calc spw23 and spw24 - based on p39 and p40

; Excitation ~3ms (to cover 4.1 ppm at 600MHz) 120 degree (Pc9_4_120.1000) pulse (##$SHAPE_INTEGFAC= 1.250034E-01):
"spw23=plw1*pow((p1/90)/((p39/120)*0.125),2)"

; Refocusing ~1ms (to cover 4.1 ppm at 600MHz) 180 degree (Rsnob.1000) pulse (##$SHAPE_INTEGFAC= 2.136957E-01):
"spw24=plw1*pow((p1/90)/((p40/180)*0.2137),2)"

#endif /* NOSP_AUTO */
; ===================================== End of Automation =====================================





1 ze 
  d11 pl26:f3
2 d1 do:f3
3 d12 pl3:f3
  50u UNBLKGRAD

  p16:gp2
  d16

  (p39:sp23 ph1):f1
  p16:gp1
  d16

#   ifdef LABEL_CN
  (center (p40:sp24 ph2):f1 (p8:sp13 ph1):f2 (DELTA1 p21 ph3 d0 p21 ph4 DELTA1):f3 )
#   else
  (center (p40:sp24 ph2):f1 (DELTA1 p21 ph3 d0 p21 ph4 DELTA1):f3 )
#   endif /*LABEL_CN*/


  DELTA2
  p16:gp1
  d16 pl26:f3
  4u BLKGRAD
  go=2 ph31 cpd3:f3 
  d1 do:f3 mc #0 to 2 
     F1PH(calph(ph3, +90), caldel(d0, +in0))
exit 
  

ph1=0 
ph2=0 
ph3=0 2
ph4=0 0 2 2 
ph31=0 2 2 0


;o2  : 13C frequency (mid of decoupling region)
;pl3 : f3 channel - power level for pulse (default)
;sp13: f2 channel - shaped pulse 180 degree (adiabatic)

;p8 : f2 channel - 180 deg shaped pulse for inversion (adiabatic)
;p16: homospoil/gradient pulse                       [1 msec]
;p21: f3 channel -  90 deg high power pulse

;p39: f1 channel - 120 deg shaped pulse for excitation (~3ms to cover 4.1 ppm at 600MHz)
;spnam39: Pc9_4_120.1000
;sp23: f1 channel - power for shaped 120 degree (Pc9_4_120.1000)

;p40: f1 channel - 180 deg shaped pulse for refocussing (~1ms to cover 4.1 ppm at 600MHz)
;spnam40: Rsnob.1000
;sp24: f1 channel - power for shaped 180 degree (Rsnob.1000)

;d0 : incremented delay (2D) = in0/2-p21*4/3.1415
;d1 : relaxation delay      [~0.1s in sofast]
;d11: delay for disk I/O                       [30 msec]
;d12: delay for power switching                [20 usec]
;d16: delay for homospoil/gradient recovery    [200 usec]
;d21 : 1/(2J)NH
;cnst4: = J(NH)      [~90 Hz]

;cnst19: Chem.shift for protons of interest (in ppm)     [~7.95 ppm for H(N)]
;cnst63: Spectral width of signals of interest (in ppm)    [~4.1 ppm for H(N)]
;cnst39: Compensation of chem. shift evolution during p39   [0.529 for Pc9_4_120.1000]

;inf1: 1/SW(N) = 2 * DW(N)
;in0: 1/ SW(N) = 2 * DW(N)
;nd0: 1
;ns: 2 * n
;ds: 16
;aq: <= 50 msec
;td1: number of experiments
;FnMODE: States-TPPI, TPPI, States or QSEC
;cpd3:    decoupling according to sequence defined by cpdprg3: garp4.p62
;cpdprg3: garp4.p62
;pcpd3:   F3 channel - 90 degree pulse for decoupling sequence (actually defined as p62)
;p62:     F3 channel - 90 degree for decoupling sequence [use >= 350 usec] (need long cuz of short d1?!)
;pl26:    F3 channel - power level for CPD/BB decoupling (p62) (low power)

;use gradient ratio:	gp 1 : gp 2
;			  11 :    7

;for z-only gradients:
;gpz1: 11%
;gpz2:  7%

;use gradient files:   
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100

;ZGOPTNS: NOAUTO - disable automatic calculation of hard pulses, power levels, delays, etc.
;ZGOPTNS: NOSP_AUTO - disable automatic calculation of SHAPED pulse lengths and power levels.

                                          ;preprocessor-flags-start
;LABEL_CN: for C-13 and N-15 labeled samples start experiment with
;             option -DLABEL_CN (eda: ZGOPTNS)
                                          ;preprocessor-flags-end

;Processing

;PHC0(F1): 90
;PHC1(F1): -180
;FCOR(F1): 1

;$Id: sfhmqcf3gpph,v 1.10.6.2 2013/03/08 15:13:06 ber Exp $
