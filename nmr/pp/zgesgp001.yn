; v001 @YaroslavN (20140806): modified -DAUTO to -DNOAUTO, etc for auto-calculation of all pulses
;zgesgp.yn - slightly automated

;zgesgp
;avance-version (12/01/11)
;1D sequence
;water suppression using excitation sculpting with gradients
;T.-L. Hwang & A.J. Shaka, J. Magn. Reson.,
;   Series A 112 275-279 (1995)
;
;$CLASS=HighRes
;$DIM=1D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"d12=20u"

"TAU=de+p1*2/3.1416+50u"

"acqt0=0"
baseopt_echo

; ===================================== Automation =====================================
#ifndef NOAUTO
; Use this section to auto-calculate parameters NOT requiring fine-tuning (HARD pulses, power levels, delays, AU-parameters, etc).

#endif /* NOAUTO */


#ifndef NOSP_AUTO
; Use this section to auto-calculate the power of SHAPED PULSES (assuming that in some cases one may want to fine-tune these)

; General procedure to calculate shaped pulse power levels.
; powerlevel [dB] = 20*log(pulseLength*SHAPE_INTEGFAC/(P90H/90*SHAPE_TOTROT))/log(10)+dBH
; TopSpin for some reason does not recognize the call to dB level of shaped pulse: "spdb". Have to calcualte via whatts.
; powerlevel [whatts] = plw1*pow( (P90H/90)/((pulseLength/SHAPE_TOTROT)*SHAPE_INTEGFAC), 2)

; excitation sculpting p12@sp1 - 180 degree "softpulse" (##$SHAPE_INTEGFAC= 1.000000E00):
"spw1=plw1*pow((p1/90)/((p12/180)*1),2)"
;"spw1=plw1*((p1/(p12/2))*(p1/(p12/2)))" ; old calc - from 750


#endif /* NOSP_AUTO */
; ===================================== End of Automation =====================================



1 ze
2 30m
  d12 pl1:f1 BLKGRAD
  d1
  p1 ph1
  
  50u UNBLKGRAD
  p16:gp1
  d16 pl0:f1
  (p12:sp1 ph2:r):f1
  4u
  d12 pl1:f1

  p2 ph3

  4u
  p16:gp1
  d16 
  TAU
  p16:gp2
  d16 pl0:f1
  (p12:sp1 ph4:r):f1
  4u
  d12 pl1:f1

  p2 ph5

  4u
  p16:gp2
  d16

  go=2 ph31
  30m mc #0 to 2 F0(zd)
  4u BLKGRAD
exit


ph1=0
ph2=0 1
ph3=2 3
ph4=0 0 1 1
ph5=2 2 3 3
ph31=0 2 2 0 


;pl0 : 0W
;pl1 : f1 channel - power level for pulse (default)
;sp1 : f1 channel - shaped pulse 180 degree
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p12: f1 channel - 180 degree shaped pulse (Squa100.1000)   [2 msec]
;spnam1 : Squa100.1000

;p16: homospoil/gradient pulse
;d1 : relaxation delay; 1-5 * T1
;d12: delay for power switching                             [20 usec]
;d16: delay for homospoil/gradient recovery
;ns: 8 * n, total number of scans: NS * TD0
;ds: 4


;use gradient ratio:    gp 1 : gp 2
;                         31 :   11

;for z-only gradients:
;gpz1: 31%
;gpz2: 11%

;use gradient files:
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100



;$Id: zgesgp,v 1.8.6.1 2012/01/31 17:56:41 ber Exp $
